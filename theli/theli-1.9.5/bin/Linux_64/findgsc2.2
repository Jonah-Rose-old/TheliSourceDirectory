#!/bin/csh -f
#++++++++++++++++
# Copyright:    (C) 2008-2015 UDS/CNRS
# License:       GNU General Public License
#.IDENTIFICATION findgsc
#.LANGUAGE       C-shell
#.AUTHOR         Francois Ochsenbein [CDS]
#.ENVIRONMENT    
#.KEYWORDS       
#.VERSION  1.0   18-Feb-1993
#.VERSION  1.1   28-Sep-1995: Now on vizir ...
#.VERSION  1.2   23-Apr-1996: New version...
#.VERSION  1.3   24-May-1999: 1.2 option
#.VERSION  2.0   11-Dec-2001; 2.2 option/version
#.VERSION  2.1   31-Jan-2002: Corrected bug for -f option
#.VERSION  2.2   31-Jan-2004: Accept file input in -g option
#.VERSION  2.3   01-Sep-2008: for proxy usage
#.PURPOSE        Find Stars in Guide Star Catalogue (GSC-I and GSC-II)
#		around a position.
#.COMMENTS       Interface to the "aclient" remote query
#	For direct queries, use
#	aclient 130.79.129.166 1660 gsc[1.1|1.2|1.3|2.2|2.3] [options]
#	   all options for each GSC version listed by -help, e.g.
#	aclient 130.79.129.166 1660 gsc2.3 -help
#  The "syscds" env. variable indicates an interactive usage (CDS)
#----------------
set D_BIN = `dirname $0`
set path = ($D_BIN /usr/local/bin /usr/bin /usr/ucb /bin)
set pgm = `basename $0`
if ($?MACHINE) then
    set path = ($path /usr/$MACHINE/bin)
endif

set acl_con = "aclient 130.79.129.161 1660"
set cgi_con = "aclient_cgi 130.79.129.161"

set rcon="$acl_con"
which aclient |& grep -v ' ' >& /dev/null # note SUN needs the grep
if( $status > 0 || $?http_proxy ) set rcon="$cgi_con"

set rgsc = "$rcon gsc"

set file = ""
set vers = "1.2"	# Default Version
set fgsc = gxtmp	# Query from File

# Default Versions
if ($0 =~ *[12].[123]) then
    set vers = `echo $0 | awk '{print substr($0,length-2)}'`
else if ($0 =~ *2) then
    set vers = 2.3
endif

MissingArgument:
if ($#argv < 1 || "$1" =~ *h*) then
    echo "Usage: $pgm [ver] J2000-center [-r radius_in_arcmin]"
    echo "   or  $pgm [ver] -g GSC-id [other_options]"
    echo "   or  $pgm [ver] -g - [other_options]     (GSC identifiers in stdin)"
    echo "   or  $pgm [ver] -f [file_with_data] [other_options]"
    echo "   or  $pgm [ver] - [other_options]            (data in stdin)"
    echo "--------------------------------------------------------------------"
    echo "Available versions:"
    echo "   1.1    1.2     1.3 (GSC-ACT)"
    echo "   2.2  (August 2001)"
    echo "   2.3  (April 2007)"
    echo "--------------------------------------------------------------------"
    echo $rgsc -help
    ${rgsc}$vers -help | tail +5
    exit 1
endif

if ("$1" =~ -1.[123]) then
    set vers = `echo "$1" | cut -c2-`
    shift
else if ("$1" =~ 1.[123]) then
    set vers = "$1"
    shift
else if ("$1" =~ ACT ) then
    set vers = 1.3
    shift
else if ("$1" =~ -2.[23]) then
    set vers = `echo "$1" | cut -c2-`
    shift
else if ("$1" =~ 2.[23]) then
    set vers = "$1"
    shift
endif

set rgsc = "${rgsc}$vers"
set fgsc = gxtmp$vers
# The arguments are different between GSC-I and GSC-II...
if ($vers =~ 1.*) then
    set rgsc  = ($rgsc:q -p 1 -h)
    set fgsc  = ($fgsc -p 1 -h)
endif
if ($#argv < 1 || "$1" =~ *h*) goto MissingArgument

#echo ....Args: $#argv = $1
switch("$1")
case "-f":
    set file = $2 ; shift
    ### NO breaksw
case "-":
    shift
    if( "$rcon" == "$cgi_con" ) then
        if( "$file" == "" ) then
            $rcon gsc - $argv:q
        else
            $rcon gsc -f $file $argv:q
        endif
        exit $status
    endif

    cat $file | $rcon putmp \; $fgsc -s $* \; rmtmp
    exit $status
case "-g":
    if ("$2" == "-") then
        echo "   (GSC-identifiers transmitted from standard input)"
	shift ; shift
        
        if( "$rcon" == "$cgi_con" ) then
            $rcon gsc - -g $argv:q 
            exit $status
        endif

	cat | $rcon putmp \; $fgsc -g $* \; rmtmp
	exit $status
    endif
    echo $rgsc $*
    $rgsc $*
    exit $status
case "-*"
    echo "$rgsc $*"
    $rgsc $argv:q
    exit $status
endsw

### Here no option -- add the Sorting and the -c
set call = "-s"
set show = cat
if ($?syscds) then
    setenv LESS '-BeiqPLine %lb (Space=next b=back q=Quit h=Help)?e (END). '
    set show = less
endif
echo  " ======== GSC$vers server  ========  Simbad, CDS, Strasbourg ========"
$rgsc $call -c $argv:q | $show
exit $status
